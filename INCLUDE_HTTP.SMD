CONSTANTS  begin
end
CLASSES BEGIN
	cHttpConstants is Struct begin
		GET_METHOD as Char default "GET"
		POST_METHOD as Char default "POST"
		PUT_METHOD as Char default "PUT"
		PATCH_METHOD as Char default "PATCH"
		DELETE_METHOD as Char default "DELETE"
		HTTP_422_UNPROCESSABLE_ENTITY as Integer default 422
		HTTP_424_FAILED_DEPENDENCY as Integer default 424
		HTTP_400_BAD_REQUEST as Integer default 400
		HTTP_200_OK as Integer default 200
		HTTP_405_METHOD_NOT_ALLOWED as Integer default 405
		HTTP_404_NOT_FOUND as Integer default 404
		HTTP_503_SERVICE_UNAVAILABLE as Integer default 503
		HTTP_401_UNAUTHORIZED as Integer default 401
		HTTP_204_NO_CONTENT as Integer default 204
		HTTP_201_CREATED as Integer default 201
		HTTP_403_FORBIDDEN as Integer default 403
		HTTP_500_INTERNAL_SERVER_ERROR as Integer default 500
	end
	cParameter is Struct begin
		Key as Char
		Value as Char
	end
	cDictionary is Array [1] of cParameter
	cRequest is Struct begin
		Url as Char
		Method as Char (10)
		private Headers as cDictionary
		private QueryParameters as cDictionary
		Body as JSON
		ResponseText as Char
		Response as JSON
		StatusResponse as Integer
		private timeElapsed as Decimal (6,3)
	end
END
objects begin
	HttpTypes AS cHttpConstants
end
CODE CLASS cParameter BEGIN
//{{CODEBEGIN

//{{CODEEND
END
CODE CLASS cDictionary BEGIN
//{{CODEBEGIN
public reset
begin
    self.Resize( 0 );
end

public add( pchrKey as char, pchrValue as char ) return boolean
begin
    if pchrKey is null or pchrKey.Trim().Length() == 0 then return false;

    self.Resize(self.Size()+1);
    self[self.Size].Key = pchrKey;
    self[self.Size].Value = pchrValue;
    
    return true;
end
//{{CODEEND
END
CODE CLASS cRequest BEGIN
//{{CODEBEGIN
public reset
begin
    self.Url = null;
    self.Method = null;
    self.ResponseText = null;
    self.Response = null;
    self.StatusResponse = null;
    self.timeElapsed = 0;
    self.Headers.reset();
    self.QueryParameters.reset();
    self.Body.Clear();
end

public traceResponse
objects
begin
    jsnTrace        as Json
end
begin
    jsnTrace.Set( "timeElapsed", self.getTimeElapsed().Char );
    jsnTrace.Set( "requestStatus", self.StatusResponse );
    if self.Response is not null then jsnTrace.Set( "requestResponse", self.Response );
    else jsnTrace.Set( "requestResponse", "null" );
    
    jsnTrace.Trace;
end

public getQueryFormed() return char
objects
begin
    intI            as integer default 0
    chrQueryFormed  as char default ''
end
begin
    if self.Url is null or self.Url.Length() == 0 then return null;

    chrQueryFormed = self.Url;

    if self.QueryParameters.Size() > 0 then
    begin
        if chrQueryFormed.Locate("?") == 0 then chrQueryFormed += "?";
        if chrQueryFormed.Locate("&") > 0 then chrQueryFormed += "&";

        for intI = 1 to self.QueryParameters.Size() do
        begin
            chrQueryFormed += self.QueryParameters[intI].Key +"="+ self.QueryParameters[intI].Value +"&";
        end

        chrQueryFormed = chrQueryFormed[1,chrQueryFormed.Length()-1];
    end

    return chrQueryFormed;
end

public send return boolean
objects
begin
    intI            as integer default 0
    axHttp          as activex 
    blnResult       as boolean default false
    decSecsInit     as decimal
end
begin
    axHttp.CreateActiveXFromProgID("Msxml2.XMLHTTP.6.0");

    if self.Url is null or self.Url.Length() == 0 then return false;

    self.Url = getQueryFormed();

    axHttp.Invoke("open",null,self.Method,self.Url,false);
    
    if self.Headers.Size() > 0 then
    begin
        for intI = 1 to self.Headers.Size() do
        begin
            axHttp.Invoke("setRequestHeader",null,self.Headers[intI].Key,self.Headers[intI].Value);
        end
    end

    decSecsInit = (GetTickCount64);
    blnResult = axHttp.Invoke("send",null,self.Body.Char);
    self.timeElapsed = ( (GetTickCount64) - decSecsInit );

    axHttp.GetProperty("responseText",self.ResponseText);
    axHttp.GetProperty("status",self.StatusResponse);
    
    self.Response.LoadFromChar( self.ResponseText );

    fixMSXMLHTTPBugs( blnResult );
    
    return blnResult;
end

public addHeader( pchrKey as char, pchrValue as char ) return boolean
begin
    return self.Headers.add( pchrKey, pchrValue );
end

public addQueryParameter( pchrKey as char, pchrValue as char ) return boolean
begin
    return self.QueryParameters.add( pchrKey, pchrValue );
end

public getTimeElapsed return decimal
begin
    return (self.timeElapsed)/1000;
end

{
    More info about this bug:
     - https://bugs.dojotoolkit.org/ticket/2418
     - https://stackoverflow.com/questions/10046972/msie-returns-status-code-of-1223-for-ajax-request
}
private fixMSXMLHTTPBugs( VAR pblnResult as boolean )
begin
    if self.StatusResponse == 1223 then
    begin
        self.StatusResponse = 204;
        pblnResult = true;
    end
end
//{{CODEEND
END
CODE BEGIN
//{{CODEBEGIN
{+--------------- GNU General Public License v3.0 ------------------+
 |                                                                  |
 | Modulo: INCLUDE_HTTP                                             |
 | Autor:  David Ropero                                             |
 | Fecha:  Septembre 2017                                           |
 |                                                                  |
 +-----------------------------------------------------------------+}

{
  Function API used to calculate elapsed time during the request.
}  

Public dll "kernel32" GetTickCount64() return integer

//{{CODEEND
END

